integrate time {ms};

shared Vn_int {mV};
shared Iion {uA/uF};
shared Vn_int_init {mV};

shared Vn_ext {mV}; 
shared In_out {mV/ms};
shared Vn_ext_init {mV};

shared Vin_int {mV};
shared Iin_Mem {mV/ms};
shared Vin_int_init {mV};

shared Vin_ext {mV};
shared Iin_Mye {mV/ms};
shared Vin_ext_init {mV};

shared E_Na {mV};
subsystem HH {
   provides Vn_diff {mV} = Vn_int - Vn_ext;
   shared Iion_area {uA/cm^2};
   provides E_R {mV} = -80;
   provides E_Na {mV} = (E_R+130{mV});
   subsystem leakage_current {
      E_L {mV} = (E_R-10{mV});
      @param g_L {mS/cm^2} = 20;
      i_L {uA/cm^2} = g_L*(Vn_diff-E_L);
      provides accum Iion_area += i_L;
   }
   subsystem slow_potassium_channel {
      shared s {1};
      subsystem slow_potassium_channel_s_gate {
         provides diffvar s {1};
         alpha_s {1/ms} = 0.3348{1/ms}/(1+exp((Vn_diff+53{mV})/(-5{mV})));
         beta_s {1/ms} = 0.03348{1/ms}/(1+exp((Vn_diff+90{mV})/(-1{mV})));
         s.init = 0.0430;
         s.diff = (alpha_s*(1-s)-beta_s*s);
      }
      E_K {mV} = (E_R-10{mV});
      g_K {mS/cm^2} = 80;
      i_K {uA/cm^2} = g_K*s*(Vn_diff-E_K);
      provides accum Iion_area += i_K;
   }

   subsystem fast_sodium_channel {
      shared m {1};
      shared h {1};
      subsystem sodium_channel_m_gate {
         provides diffvar m {1};
         alpha_m {1/ms} = (fabs(-(Vn_diff+21.4{mV})/10.3{mV}) < 0.000001) ? (7.11{1/mV/ms}*10.3{mV}) : (7.11{1/mV/ms}*(Vn_diff+21.4{mV})/(1{1}-exp(-(Vn_diff+21.4{mV})/10.3{mV})));
         beta_m {1/ms} = (fabs((Vn_diff+25.7{mV})/9.16{mV}) < 0.000001) ? (0.329{1/mV/ms}*9.16{mV}) : (0.329{1/mV/ms}*(-(Vn_diff+25.7{mV}))/(1{1}-exp((Vn_diff+25.7{mV})/9.16{mV})));
         m.init = 0.0732;
         m.diff = (alpha_m*(1-m)-beta_m*m);
      }
      subsystem sodium_channel_h_gate {
         provides diffvar h {1};
         alpha_h {1/ms} = (fabs((Vn_diff+114{mV})/11{mV}) < 0.000001) ? (0.38{1/mV/ms}*11{mV}) : (0.38{1/mV/ms}*(-(Vn_diff+114{mV}))/(1{1}-exp((Vn_diff+114{mV})/11{mV})));
         beta_h {1/ms} = 14.1{1/ms}/(1{1}+exp((Vn_diff+31.8{mV})/(-13.4{mV})));
         h.init = 0.6207;
         h.diff = (alpha_h*(1-h)-beta_h*h);
      }
      g_Naf {mS/cm^2} = 3000;
      i_Naf {uA/cm^2} = g_Naf*m^3*h*(Vn_diff-E_Na);
      provides accum Iion_area += i_Naf;
   }
   
   subsystem pers_sodium_channel {
      shared p {1};
      subsystem pers_sodium_channel_p_gate {
         provides diffvar p {1};
         alpha_p {1/ms} = (fabs((Vn_diff+27{mV})/10.2{mV}) < 0.000001{1}) ? (0.0382{1/mV/ms}*10.2{mV}) : (0.0382{1/mV/ms}*(Vn_diff+27{mV})/(1{1}-exp(-(Vn_diff+27{mV})/10.2{mV}))) ;
         beta_p {1/ms} = (fabs((Vn_diff+34{mV})/10{mV}) < 0.000001{1}) ? (0.000955{1/mV/ms}*10{mV}) : (0.000955{1/mV/ms}*(-(Vn_diff+34{mV}))/(1{1}-exp((Vn_diff+34{mV})/10{mV})));
         p.init = 0.2026;
         p.diff = (alpha_p*(1-p)-beta_p*p);
      }
      g_Nap {mS/cm^2} = 10;
      i_Nap {uA/cm^2} = g_Nap*p^3*(Vn_diff-E_Na);
      provides accum Iion_area += i_Nap;
   }

   subsystem nodal_current{
      provides accum In_out {mV/ms} += Vn_ext*1000;
   }

   provides Vn_int_init = -80;
   provides Vn_ext_init = 0;
   @param Cm {uF/cm^2} = 2;
   provides accum Iion += Iion_area/Cm;
   
}


subsystem internodal {

   subsystem myelin_current{      
      provides g_Mye {mS/cm^2} = 1/240;
      provides C_Mye {uF/cm^2} = 0.1/240;
      provides accum Iin_Mye {mV/ms} += (Vin_ext*g_Mye)/C_Mye;      
   }

   subsystem membrane_current{
      provides V_Res {mV} = -80;
      provides g_Mem {mS/cm^2} = 0.00033;
      provides C_Mem {uF/cm^2} = 2*0.33;
      provides accum Iin_Mem {mV/ms} += ((Vin_int+80)*g_Mem)/C_Mem;
   }

   provides Vin_ext_init {mV} = 0;
   provides Vin_int_init {mV} = -80;
   
}

subsystem McIntyreModel {
  use HH;
  use internodal;
}